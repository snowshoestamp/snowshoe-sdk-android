apply plugin: 'com.android.library'

ext {
  bintrayRepo = 'maven'
  bintrayName = 'snowshoe-stamp-sdk'

  publishedGroupId = 'com.mattluedke'
  libraryName = 'snowshoelib'
  artifact = 'snowshoelib'

  libraryDescription = 'SnowShoeStamp Client Api Library'

  siteUrl = 'https://github.com/snowshoestamp/snowshoe-sdk-android'
  gitUrl = 'https://github.com/snowshoestamp/snowshoe-sdk-android.git'

  libraryVersion = '3.0.3'

  developerId = 'snowshoestamp'
  developerName = 'snowshoe stamp'
  developerEmail = 'engineering@snowshoestamp.com'

  licenseName = 'MIT'
  licenseUrl = 'https://github.com/snowshoestamp/snowshoe-sdk-android/blob/master/LICENSE'
  allLicenses = ["MIT"]
}

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 2
    versionName "3.0.3"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.github.scribejava:scribejava-apis:6.8.1'
  implementation 'com.android.volley:volley:1.1.1'
}

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId                               // Maven Group ID for the artifact

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        groupId 'com.mattluedke'
        artifactId 'snowshoelib'

        // Add your description here
        name 'snowshoe-stamp-sdk'
        description 'SnowShoeStamp Client Api Library'
        url 'https://github.com/snowshoestamp/snowshoe-sdk-android'

        // Set your license
        licenses {
          license {
            name 'MIT'
            url 'https://github.com/snowshoestamp/snowshoe-sdk-android/blob/master/LICENSE'
          }
        }
        developers {
          developer {
            id 'snowshoestamp'
            name 'Snowshoe Stamp'
            email 'engineering@snowshoestamp.com'
          }
        }
        scm {
          connection 'https://github.com/snowshoestamp/snowshoe-sdk-android.git'
          developerConnection 'https://github.com/snowshoestamp/snowshoe-sdk-android.git'
          url 'https://github.com/snowshoestamp/snowshoe-sdk-android'
        }
      }
    }
  }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']
  pkg {
    repo = bintrayRepo
    name = bintrayName
    desc = libraryDescription
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = allLicenses
    publish = true
    publicDownloadNumbers = true
    version {
      desc = libraryDescription
      gpg {
        sign = false
      }
    }
  }
}
